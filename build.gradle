buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = minecraft_version + "-" + mod_version
group = "net.mcft.copy.copycore"
archivesBaseName = "copycore"

minecraft {
    version = minecraft_version + "-" + forge_version

    replaceIn project.projectDir.name + ".java"
    replace "@VERSION@", project.mod_version
}

sourceSets.main {
    java { srcDirs 'src' }
    resources { srcDirs 'resources' }
}

processResources {
    inputs.property "version", project.mod_version
    inputs.property "mcversion", project.minecraft_version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.mod_version, 'mcversion':project.minecraft_version
    }
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
}
